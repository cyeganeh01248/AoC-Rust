default: run

test DAY="":
    #!/usr/bin/env bash
    day={{DAY}}

    if [ "$day" == "" ]; then
        day=$(ls src | grep day | sed 's/day//' | sed 's/.rs//' | sort -h -r | head -n 1)
    fi
    cargo test day${day}

run DAY="":
    #!/usr/bin/env bash
    day={{DAY}}

    if [ "$day" == "" ]; then
        day=$(ls src | grep day | sed 's/day//' | sed 's/.rs//' | sort -h -r | head -n 1)
    fi
    cargo aoc input -d $day -y 2023
    cargo aoc -d $day

bench DAY="":
    #!/usr/bin/env bash
    day={{DAY}}

    if [ "$day" == "" ]; then
        day=$(ls src | grep day | sed 's/day//' | sed 's/.rs//' | sort -h -r | head -n 1)
    fi

    cargo aoc input -d $day -y 2023
    cargo aoc bench -d $day

gen DAY="":
    #!/usr/bin/env bash
    day={{DAY}}

    if [ "$day" == "" ]; then
        day=$(date | awk '{print $3}')
    fi
    cargo aoc input -g -d $day -y 2023

test-all:
    cargo test day

run-all:
    #!/usr/bin/env bash
    for day in $(ls src | grep day | sed 's/day//' | sed 's/.rs//' | sort -h); do
        echo -------------- AoC Day $day --------------
        cargo aoc input -d $day -y 2023
        cargo aoc -d $day
    done

bench-all:
    #!/usr/bin/env bash
    for day in $(ls src | grep day | sed 's/day//' | sed 's/.rs//' | sort -h); do
        echo -------------- AoC Day $day --------------
        cargo aoc input -d $day -y 2023
        cargo aoc bench -d $day
    done

fetch-inputs:
    #!/usr/bin/env bash
    for day in $(ls src | grep day | sed 's/day//' | sed 's/.rs//' | sort -h); do
        echo -------------- AoC Day $day --------------
        cargo aoc input -d $day -y 2023 &> /dev/null
        if [ $? -ne 0 ]; then
            echo "Failed to fetch input for day $day"
        else
            echo "Fetched input for day $day"
        fi
    done

clean:
    cargo clean

lint:
    cargo fmt
    cargo clippy --fix --allow-dirty --allow-staged

[confirm("Are you sure you want to commit for the day?")]
git-complete-day DAY:
    #!/usr/bin/env bash
    git add . && \
    git commit -m "Completed day {{DAY}} 2023" && \
    git push
